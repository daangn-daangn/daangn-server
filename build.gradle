buildscript {
	ext {
		springBootVersion = '2.6.4'
		dependencyManagementVersion = '1.0.11.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}"
	}
}


subprojects {
	group = 'com.daangn-daangn'
	version '1.0'

	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	sourceCompatibility = 11

	repositories {
		mavenCentral()
	}

	// 프로젝트 개발에 공통으로 필요한 의존성 선언
	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		developmentOnly 'org.springframework.boot:spring-boot-devtools'
		implementation 'org.springframework.boot:spring-boot-starter'
		implementation 'org.springframework.boot:spring-boot-starter-test'
		implementation 'org.springframework.boot:spring-boot-starter-validation'
		implementation group: 'org.apache.commons', name: 'commons-lang3'

		//querydsl 관련 추가
		implementation "com.querydsl:querydsl-core"
		implementation "com.querydsl:querydsl-jpa"
		annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
		annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
		annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
		//querydsl 관련 추가 끝
	}
}

project(':api-server') {
	dependencies {
		api project(':common')
	}
}

project(':chat-server') {
	dependencies {
		api project(':common')
	}
}